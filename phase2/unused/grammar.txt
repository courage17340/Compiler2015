program
dec_or_func dec_or_funcs
\\
dec_or_funcs
'' \
dec_or_func dec_or_funcs
\\
dec_or_func
declaration \
function_definition
\\
declaration
type_specifier ';' \
type_specifier init_declarators ';'
\\
function_definition
type_specifier plain_declarator '(' ')' compound_statement \
type_specifier plain_declarator '(' parameters ')' compound_statement
\\
parameters
plain_declaration comma_plain_declarations
\\
comma_plain_declarations
'' \
',' plain_declaration comma_plain_declarations
\\
declarators
declarator comma_declarators
\\
comma_declarators
'' \
',' declarator comma_declarators
\\
init_declarators
init_declarator comma_init_declarators
\\
comma_init_declarators
'' \
',' init_declarator comma_init_declarators
\\
init_declarator
declarator \
declarator '=' initializer
\\
initializer
assignment_expression \
'{' initializer comma_initializers '}'
\\
comma_initializers
'' \
',' initializer comma_initializers
\\
type_specifier
'void' \
'char' \
'int' \
struct_or_union identifier \
struct_or_union '{' type_specifiers '}' \
struct_or_union identifier '{' type_specifiers '}'
\\
type_specifiers
type_specifier declarators comma_type_specifiers
\\
comma_type_specifiers
'' \
';' type_specifier declarators comma_type_specifiers
\\
struct_or_union
'struct' \
'union'
\\
plain_declaration
type_specifier declarator
\\
declarator
plain_declarator '(' ')' \
plain_declarator '(' parameters ')' \
plain_declarator comma_array_sizes
\\
comma_array_sizes
'' \
'[' constant_expression ']' comma_array_sizes
\\
plain_declarator
identifier \
'*' plain_declarator
\\
statement
matched_statement \
open_statement
\\
matched_statement
'if' '(' expression ')' matched_statement 'else' matched_statement \
expression_statement \
compound_statement \
iteration_statement \
jump_statement
\\
open_statement
'if' '(' expression ')' statement \
'if' '(' expression ')' matched_statement 'else' open_statement
\\
expression_statement
';' \
expression ';'
\\
compound_statement
'{' comma_declarations comma_statements '}'
\\
comma_declarations
'' \
declaration comma_declarations
\\
comma_statements
'' \
statement comma_statements
\\
iteration_statement
'while' '(' expression ')' statement \
'for' '(' ';' ';' ')' statement \
'for' '(' ';' ';' statement ')' statement \
'for' '(' ';' statement ';' ')' statement \
'for' '(' ';' statement ';' statement ')' statement \
'for' '(' statement ';' ';' ')' statement \
'for' '(' statement ';' ';' statement ')' statement \
'for' '(' statement ';' statement ';' ')' statement \
'for' '(' statement ';' statement ';' statement ')' statement
\\
jump_statement
'continue' ';' \
'break' ';' \
'return' ';' \
'return' expression ';'
\\
expression
assignment_expression comma_assignment_expressions
\\
comma_assignment_expressions
'' \
',' assignment_expression comma_assignment_expressions
\\
assignment_expression
logical_or_expression \
unary_expression assignment_operator assignment_expression
\\
assignment_operator
'=' \
'*=' \
'/=' \
'%=' \
'+=' \
'-=' \
'<<=' \
'>>=' \
'&=' \
'^=' \
'|='
\\
constant_expression
logical_or_expression
\\
logical_or_expression
logical_and_expression comma_logical_and_expressions
\\
comma_logical_and_expressions
'' \
'||' logical_and_expression comma_logical_and_expressions
\\
logical_and_expression
inclusive_or_expression comma_inclusive_or_expressions
\\
comma_inclusive_or_expressions
'' \
'&&' inclusive_or_expression comma_inclusive_or_expressions
\\
inclusive_or_expression
exclusive_or_expression comma_exclusive_or_expressions
\\
comma_exclusive_or_expressions
'' \
'|' exclusive_or_expression comma_exclusive_or_expressions
\\
exclusive_or_expression
and_expression comma_and_expressions
\\
comma_and_expressions
'' \
'^' and_expression comma_and_expressions
\\
and_expression
equality_expression comma_equality_expressions
\\
comma_equality_expressions
'' \
'&' equality_expression comma_equality_expressions
\\
equality_expression
relational_expression comma_relational_expressions
\\
comma_relational_expressions
'' \
equality_operator relational_expression comma_relational_expressions
\\
equality_operator
'==' \
'!='
\\
relational_expression
shift_expression comma_shift_expressions
\\
comma_shift_expressions
'' \
relational_operator shift_expression comma_shift_expressions
\\
relational_operator
'<' \
'>' \
'<=' \
'>='
\\
shift_expression
additive_expression comma_additive_expressions
\\
comma_additive_expressions
'' \
shift_operator additive_expression comma_additive_expressions
\\
shift_operator
'<<' \
'>>'
\\
additive_expression
multiplicative_expression comma_multiplicative_expressions
\\
comma_multiplicative_expressions
'' \
additive_operator multiplicative_expression comma_multiplicative_expressions
\\
additive_operator
'+' \
'-'
\\
multiplicative_expression
cast_expression comma_cast_expressions
\\
comma_cast_expressions
'' \
multiplicative_operator cast_expression comma_cast_expressions
\\
multiplicative_operator
'*' \
'/' \
'%'
\\
cast_expression
unary_expression \
'(' type_name ')' cast_expression
\\
type_name
type_specifier pointers
\\
pointers
'' \
'*' pointers
\\
unary_expression
postfix_expression \
'++' unary_expression \
'--' unary_expression \
unary_operator cast_expression \
'sizeof' unary_expression \
'sizeof' '(' type_name ')'
\\
unary_operator
'&' \
'*' \
'+' \
'-' \
'~' \
'!'
\\
postfix_expression
primary_expression postfixes
\\
postfixes
'' \
postfix postfixes
\\
postfix
'[' expression ']' \
'(' ')' \
'(' arguments ')' \
'.' identifier \
'->' identifier \
'++' \
'--'
\\
arguments
expression
\\
primary_expression
identifier \
constant \
string \
'(' expression ')'
\\
constant
integer_constant \
character_constant
\\
identifier
'identifier'
\\
integer_constant
'integer_constant'
\\
character_constant
'character_constant'
\\
string
'string'
\\